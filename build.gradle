plugins {
    // The following line allows to load io.gatling.gradle plugin and directly apply it
    id 'io.gatling.gradle' version '3.6.1'
}

gatling {
  // WARNING: options below only work when logback config file isn't provided
  logLevel = 'WARN' // logback root level
  logHttp = 'NONE' // set to 'ALL' for all HTTP traffic in TRACE, 'FAILURES' for failed HTTP traffic in DEBUG
}

/* maven central is added by default, uncomment and add more custom repositories
repositories {
    mavenCentral()
}
*/

configurations {
  fatJarDependencies.extendsFrom gatling
}

task fatJar(type: Jar, dependsOn: [':gatlingClasses', ':processResources']) {
  manifest {
    attributes 'Implementation-Title': 'Preparing test',
      'Implementation-Version': '',
      'Main-Class': 'io.gatling.app.Gatling'
  }
  classifier = 'all'
  from files(sourceSets.main.output.classesDirs)
  from files(sourceSets.gatling.output)
  from { configurations.fatJarDependencies.collect { it.isDirectory() ? it : zipTree(it) } } {
    exclude 'META-INF/MANIFEST.MF'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
  }
  with jar
}
